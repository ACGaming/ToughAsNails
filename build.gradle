buildscript {
    repositories {
        jcenter()
		mavenLocal()
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
		maven {
            name = 'sponge'
            url = 'http://repo.spongepowered.org/maven'
        }
    }
	dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.3-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'maven'

ext {
    packaging = 'jar'
	configFile = file "build.properties"
}

configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}


group = "glitchfiend.toughasnails"
version = config.mod_version
archivesBaseName = "ToughAsNails"

repositories {
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/maven/'
    }
}

//Add "embedded" configuration
configurations {
    embed
    compile.extendsFrom(embed)
}

dependencies {
    embed('org.spongepowered:mixin:0.5.3-SNAPSHOT') {
		transitive = false
    }
}

minecraft {
    version = config.minecraft_version + "-" + config.forge_version
	mappings = project.config.mappings_version
}

jar {
	//Add dependencies to the JAR
	from configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }

	manifest {
		attributes (
			"TweakClass": "toughasnails.launch.TANLaunchTweaker",
			"TweakOrder": 0,
			"MixinConfigs": "mixins.toughasnails.json",
			"FMLCorePluginContainsFMLMod": "true",
			"ForceLoadAsMod": true,
			"FMLAT": "toughasnails_at.cfg"
		)
	}
}